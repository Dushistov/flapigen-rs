apply plugin: 'com.android.application'

def buildType

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "net.akaame.myapplication"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/libs'
        }
    }

    buildTypes {
        debug {
            buildType = 'debug'
        }
        
        release {
            buildType = 'release'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api 'com.android.support:appcompat-v7:28.0.0'
    api 'com.android.support.constraint:constraint-layout:1.1.3'
    api 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
}

// ANCHOR:cargo
def rustBasePath = ".."
def archTriplets = [
    'armeabi': 'arm-linux-androideabi',
    'arm64': 'aarch64-linux-android',
]

archTriplets.each { arch, target ->
    // execute cargo metadata and get path to target directory
    tasks.create(name: "cargo-output-dir-${arch}-${buildType}", description: "Get cargo metadata") {
        new ByteArrayOutputStream().withStream { os ->
            exec {
                commandLine 'cargo', 'metadata', '--format-version', '1'
                workingDir rustBasePath
                standardOutput = os
            }
            def outputAsString = os.toString()
            def json = new groovy.json.JsonSlurper().parseText(outputAsString)

            logger.info("cargo target directory: ${json.target_directory}")
            project.ext.cargo_target_directory = json.target_directory
        }
    }
    // Build with cargo
    tasks.create(name: "cargo-build-${arch}-${buildType}", type: Exec, description: "Building core for ${arch}-${buildType}", dependsOn: "cargo-output-dir-${arch}-${buildType}") {
        workingDir rustBasePath
        
        if (buildType == 'release') {
            commandLine 'cargo', 'build', "--target=${target}", '--release'
        } else {
            commandLine 'cargo', 'build', "--target=${target}"
        }
    }
    // Sync shared native dependencies
    tasks.create(name: "sync-rust-deps-${arch}-${buildType}", type: Sync, dependsOn: "cargo-build-${arch}-${buildType}") {
        from "${rustBasePath}/src/libs/${arch}-${buildType}"
        include "*.so"
        into "src/main/libs/${arch}"
    }
    // Copy build libs into this app's libs directory
    tasks.create(name: "rust-deploy-${arch}-${buildType}", type: Copy, dependsOn: "sync-rust-deps-${arch}-${buildType}", description: "Copy rust libs for (${arch}-${buildType}) to jniLibs") {
        from "${project.ext.cargo_target_directory}/${target}/${buildType}"
        include "*.so"
        into "src/main/libs/${arch}"
    }

    // Hook up tasks to execute before building java
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn "rust-deploy-${arch}-${buildType}"
    }
    preBuild.dependsOn "rust-deploy-${arch}-${buildType}"

    // Hook up clean tasks
    tasks.create(name: "clean-${arch}-${buildType}", type: Delete, description: "Deleting built libs for ${arch}-${buildType}", dependsOn: "cargo-output-dir-${arch}-${buildType}") {
        delete fileTree("${project.ext.cargo_target_directory}/${target}/${buildType}") {
            include '*.so'
        }
    }
    clean.dependsOn "clean-${arch}-${buildType}"
}
// ANCHOR_END:cargo
